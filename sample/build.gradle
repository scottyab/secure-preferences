apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'

repositories {
    google()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
    }
}

android {
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "SecurePreferencesSample-${variant.name}-${variant.versionName}.apk"
        }
    }

    compileSdkVersion 27
    buildToolsVersion '26.0.3'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 8
        versionName "1.0.8"
    }

    //check if the keystore details are defined in gradle.properties (this is so the key is not in github)
    if(project.hasProperty("SECURE_PREF_SAMPLE_STORE")) {
        signingConfigs {
            //from local gradle.properties
            release {
                storeFile file(SECURE_PREF_SAMPLE_STORE)
                keyAlias SECURE_PREF_SAMPLE_KEY
                storePassword SECURE_PREF_SAMPLE_STORE_PASS
                keyPassword SECURE_PREF_SAMPLE_KEY_PASS
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


    buildTypes {
        debug{
            debuggable true
            applicationIdSuffix ".debug"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }

        release {
            minifyEnabled true
            if(project.hasProperty("SECURE_PREF_SAMPLE_STORE")) {
                signingConfig signingConfigs.release
            }
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
    }


    dependencies {
        implementation 'com.android.support:appcompat-v7:27.0.2'
        implementation "com.android.support:design:27.0.2"

        implementation 'com.facebook.stetho:stetho:1.5.0'

        implementation "com.jakewharton:butterknife:8.8.1"
        annotationProcessor "com.jakewharton:butterknife-compiler:8.8.1"

        //compile 'com.scottyab:secure-preferences-lib:0.1.1'
        //snapshot
        //compile('com.scottyab:secure-preferences-lib:0.1.1-SNAPSHOT')

        //used when using local aes-crypto
        //compile project(':aes-crypto-release')
        implementation project(':library')
    }
}
